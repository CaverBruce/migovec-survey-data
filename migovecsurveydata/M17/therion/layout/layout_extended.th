encoding  utf-8
layout extended
  scale 1 400
  colour map-fg [96 90 69]
  map-header 0 0 off
   
  
  grid-coords border

    #PDF DOCUMENTION
    code tex-map
      \cavename={Eisgruben Eish√∂hle}
      \comment{Kat 1611/39}
    endcode  
    
    code metapost
  def a_ice_MY (expr p) =
  T:=identity;
  thclean p;
  thfill p withcolor (0.72, 0.88, 0.95);
  pickup PenC;
  path q; q = bbox p;
  picture tmp_pic, tmp_ice;
  pair z;
  tmp_ice := image (
    draw (-.13u,0)--(.13u,0);
    draw (0,-.13u)--(0,.13u);
  );
  tmp_pic := image(
    for i = xpart llcorner q step 1.5u until xpart urcorner q:
      for j = ypart llcorner q step 1.5u until ypart urcorner q:
        z := ((i,j) randomized 1.3u);
	if xpart (p intersectiontimes ((bbox tmp_ice) shifted z)) < 0:
	  draw tmp_ice shifted z;
	fi;
      endfor;
    endfor;
  );
  clip tmp_pic to p;
  drawoptions();
  draw tmp_pic;
enddef;

def a_snow_MY (expr P) =
  T:=identity;
  thclean P;
  thfill P withcolor (0.96, 0.96, 1);
  
enddef;

def l_survey_cave_MY (expr P) =
  T:=identity;
  draw P withpen PenD withcolor (0.7,0,0);
enddef;
 
 initsymbol("a_snow_MY");
  initsymbol("a_ice_MY");
  initsymbol("l_survey_cave_MY");


  def p_u_logger_MY (expr P,R,S,A)=
    U:=(.2u,.2u);
    T:=identity aligned A rotated R scaled S shifted P;
    thdraw (-.3u,-.3u)--(-.3u,.3u)--(.3u,.3u)..(.3u,-.3u)--cycle;
    thfill (-.2u,-.2u)--(-.2u,.2u)--(.2u,.2u)..(.2u,-.2u)--cycle;
  enddef;
  initsymbol("p_u_logger_MY");

    def p_station_temporary_MY (expr pos) =
  T:= identity  shifted pos;
  pickup  PenD;
  thdraw withcolor (0.7,0,0) (-.1u,-.1u)--(.1u,-.1u) -- (.1u,.1u) -- (-.1u,.1u) -- cycle;
  thdraw origin;
enddef;
initsymbol("p_station_temporary_MY");

def l_u_openshaft_MY (expr P) =
  T:=identity;
  cas := 0;
  dlzka := arclength P;
  mojkrok:=adjust_step(dlzka, 0.3u);
  pickup PenC;
  t1:=0;
  forever:
    t := arctime (cas + mojkrok/2) of P;
    t2 := arctime (cas + mojkrok) of P;
    thfill (point t1 of P + .1u *unitvector(thdir(P,t1) rotated 90)) -- 
      ((point t of P) + .4u * unitvector(thdir(P,t) rotated 90)) -- (point t2 of P + .1u *unitvector (thdir(P,t2) rotated 90)) --
      cycle;
      thdraw (point t1 of P + .1u *unitvector(thdir(P,t1) rotated 90)) .. (point t2 of P + .1u *unitvector (thdir(P,t2) rotated 90));
    cas := cas + mojkrok;
    exitif cas > dlzka - (2*mojkrok/3); % for rounding errors
    t1:=t2;
  endfor;
  thdraw P;
enddef;
initsymbol ("l_u_openshaft_MY")



def l_u_ancient_MY (expr P) =
  T:=identity;
  pickup PenC;
  thdraw P dashed evenly scaled optical_zoom;
enddef;
  initsymbol ("l_u_ancient_MY")

  endcode


  
 symbol-assign line slope BCRA
 symbol-assign area ice MY
 symbol-assign area snow MY
 debug station-names
 symbol-hide line survey

endlayout

  text en "line u:openshaft_MY" "open shaft"
  text en "area u:block_MY" "Block"
  text en "point u:logger_MY"  "logger"
  text en "point u:CCC_MY" "CCC"
  text en "line u:ancient_MY" "1982 firn and ice levels"

