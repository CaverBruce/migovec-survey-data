layout custom_scalebar_arrow
	code metapost
	    #(contributions of Thomas Holder)
		
		def s_northarrow (expr rot) =
		  	begingroup
		    	interim defaultscale:=0.5; % scale your north arrow here
		    	T:=identity scaled defaultscale rotated -rot;
		    	pickup pencircle scaled (0.08cm * defaultscale);
		    	thdraw (-.4cm,-1.4cm)--(0,2.8cm)--(.4cm,-1.4cm)--cycle;
		    	p:=fullcircle scaled 1.6cm;
		    	thclean p; thdraw p;
		    	p:=(0.95cm,0)--(0.65cm,0);
		    	thdraw p; thdraw p xscaled -1;
		    	pickup pencircle scaled (0.12cm * defaultscale);
		    	p:=(0.28cm,0.42cm);
		    	thdraw p--(p yscaled -1)--(p xscaled -1)--(p scaled -1);
		  	endgroup;
		enddef;

  		def s_scalebar (expr l, units, txt) =
  		  	begingroup
  		   		interim warningcheck:=0;
  		   		tmp40:=40 * (l / Scale * cm * units / 100);
  		   		tmp80:=80 * (l / Scale * cm * units / 100);
  		   		tmp120:=120 * (l / Scale * cm * units / 100);
  		   		tmp100:=100 * (l / Scale * cm * units / 100);
  		   		tmp160:=160 * (l / Scale * cm * units / 100);
  		   		tmp200:=200 * (l / Scale * cm * units / 100);
  		   		scal10:=10 * l / 100;
  		   		scal40:=40 * l / 100;
  		   		scal80:=80 * l / 100;
  		   		scal120:=120 * l /100;
  		   		scal160:=160 * l /100;
  		   		scal200:=200 * l /100;
  		   		brht:= 5bp;
  		   		lblht:= 8bp;
  		  	endgroup;

  		  	pickup PenD;

  		  	draw (0,0)--(0,brht)--(tmp200,brht)--(tmp200,0)--(0,0);
  		  	draw (tmp40,0)--(tmp40,brht);
  		  	draw (tmp80,0)--(tmp80,brht);
  		  	draw (tmp120,0)--(tmp120,brht);
  		  	draw (tmp160,0)--(tmp160,brht);
  		  	fill (tmp40,0)--(tmp80,0)--(tmp80,brht)--(tmp40,brht)--cycle;
  		  	fill (tmp120,0)--(tmp160,0)--(tmp160,brht)--(tmp120,brht)--cycle;


  		 	begingroup
  		 		label.top(thTEX(decimal (2*l) & "\thinspace" & txt),origin+(tmp200,lblht));
  		 		label.top(thTEX(decimal (scal160)),origin+(tmp160,lblht));
  		 		label.top(thTEX(decimal (scal120)),origin+(tmp120,lblht));
  		 		label.top(thTEX(decimal (scal80)),origin+(tmp80,lblht));
  		 		label.top(thTEX(decimal (scal40)),origin+(tmp40,lblht));
  		 		label.top(thTEX(decimal (0)),origin+(0,lblht));
  		 		#label.bot(thTEX("PLAN 1 : " & decimal round(Scale*100)),(tmp80+scal10,-2lblht));
  		 	endgroup
  		enddef; 
   endcode
endlayout